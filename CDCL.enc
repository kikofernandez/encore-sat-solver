module CDCL

import Util
import Vector
import VVector
import Clause
import Instance
import Assignments
import Heuristic
import ImplicationGraph
import Array
import LocalArray
import Solver

active class CDCL
-- private attributes
  val heuristics : Heuristic
  var assignments : Assignments
  val instance : Instance
  var dL : int = 0
  var nConflicts : int = 0
  var nReceivedConflicts : int = 0
  val originalNOfClauses : int
  var latestConflictClause : Clause
  val solverId : int

  val nVars : int
  val varsToAssign : RArray[int]


  def addConflictClause(conflictClause : Clause) : unit
    this.instance.addClause(conflictClause)
    this.nReceivedConflicts += 1
    this.heuristics.addConflictClause(conflictClause)
  end

  def getAllConflicts() : (int,int)
    (this.nConflicts, this.nReceivedConflicts)
  end

  def init(clauses : RVector[Clause], nVars : int, varsToAssign : RArray[int],
           var heuristics : Heuristic, solverId : int) : unit
    this.assignments = new Assignments(nVars*2, varsToAssign)
    this.originalNOfClauses = clauses.size()
    this.instance = new Instance(clauses, nVars*2)
    this.heuristics = consume heuristics
    this.solverId = solverId
    this.nVars = nVars
    this.varsToAssign = varsToAssign
  end

  -- public methods
  def getAssignments() : FinalAssignment
    this.assignments.finalAssignment()
  end

  def restart() : unit
    println("Solver {} restarted.", this.solverId)
    this.dL = 0
    this.assignments = new Assignments(this.nVars*2, this.varsToAssign)
  end

  def getLatestConflictClause() : Clause
      this.latestConflictClause
  end

  def getSetVars() : int
    this.assignments.nSetVars
  end

  def solveProtocol3(s: Solver): unit
    WAMP("solveProtocol3")
    s!solveProtocol4(this.solve())
  end

  def getLatestConflictClauseProtocol5(solver: Solver, s: bool, d: bool): unit
    WAMP("solveProtocol5")
    solver!solveProtocol6(this.getLatestConflictClause(), s, d)
  end

  def solve() : (bool, bool, bool)
    WAMP("In CDCL.solve()")
    var solveable = true
    var done = false
    var conflict = false
    if (this.unitPropagation(this.dL)) then -- NO CONFLICT
      if (this.allVariablesAssigned()) then
        done = true
      else
        val indexAndLiteral = this.heuristics.getVariableSelection(this.assignments.getVariables(), this.instance.clauses)
        val varIndex = indexAndLiteral.0
        val value = this.heuristics.getValueSelection(indexAndLiteral.1)
        this.dL += 1
        this.assignments.guessVariable(varIndex, value, this.dL)
      end
    else -- CONFLICT
      conflict = true
      this.nConflicts += 1

      if (this.nConflicts % 100 == 0) then
        println("Number of conflicts: {}", this.nConflicts)
      end

      val backtrackDL = this.conflictAnalysis(this.dL)
      if (backtrackDL < 0) then
        solveable = false
      else
        this.backtrack(backtrackDL)
        this.dL = backtrackDL
      end
    end
    WAMP("Leaving CDCL.solve()")
    (solveable, done, conflict)
  end

  def checkCorrectness() : bool
    WAMP("In CDCL.checkCorrectness()")
    val variables = this.assignments.variables
    var i = 0
    var iLimit = variables.size()
    while (i < iLimit) do
      val var1 = variables.at(i)
      val var2 = variables.at(i+1)
      if ((var1.value == -1) || (var2.value == -1) || (var1.value != var2.value)) then
        return false
      end
      i += 2
    end
    i = 0
    iLimit = this.instance.clauses.size()

    -- NOTE: getVariables creates a copy of half the items
    val variables = this.assignments.getVariables()

    while (i < iLimit) do
      val literals = this.instance.clauses.at(i).literals
      var satisfied = false
      var j = 0
      val jLimit = literals.size()
      while(j < jLimit) do
        val literal = literals.at(j)
        val index = literalToVarIndex(literal)
        val vara = variables.at(index)
        if (literal % 2 + vara.value == 1) then
          satisfied = true
          break
        end
        j += 1
      end
      if (not satisfied) then
        return false
      end
      i += 1
    end
    WAMP("Leaving CDCL.checkCorrectness()")
    return true
  end

  def backtrack(dL : int) : unit
    WAMP("In CDCL.backtrack()")
    val variables = this.assignments.getVariables()
    val implicationGraph = this.instance.implicationGraph.implicationGraph
    repeat i <- implicationGraph.size() do
      val implications = implicationGraph.at(i)
      var j = implications.size() - 1
      val jLimit = 0
      while (j >= 0) do
        if (implications.at(j).dL > dL) then
          implications.remove(j)
        end
        j -= 1
      end
    end
    repeat i <- variables.size() do
      val vara = variables.at(i)
      if (vara.dL > dL) then
        this.assignments.resetVar(i)
      end
    end
    WAMP("Leaving CDCL.backtrack()")
  end

  def allVariablesAssigned() : bool
    this.instance.nVars == this.assignments.nSetVars
  end

  -- TODO: (kiko) look at it in a bigger screen
  -- returns false if conflict is found, otherwise true
  def private unitPropagation(dL : int) : bool
    WAMP("In CDCL.unitPropagation()")
    val literalsToAssign = new Vector[int](10)
    var popIndex = 0
    var popCounter = 0
    var firstLoop = true
    var didPropagate = true -- set to true just to enter the loop at least once
    var noConflict = true
    val variables = this.assignments.variables
    while (((popCounter > 0) || firstLoop)) do
      if (not firstLoop) then
        didPropagate = true
        this.assignments.assignCounterpart(literalsToAssign.at(popIndex))
        popIndex += 1
        popCounter -= 1
      end
      firstLoop = false
      while (didPropagate && noConflict) do
        didPropagate = false
        var i = 0
        val iLimit = this.instance.clauses.size()
        while (noConflict && (i < iLimit) && (not didPropagate)) do
          val clause = this.instance.clauses.at(i)
          var literalToProp = -1
          val unitClauseV = new Vector[int](clause.literals.size())
          var j = 0
          val jLimit = clause.literals.size()
          var loop = true
          while (loop && (j < jLimit)) do
            val literal = clause.literals.at(j)
            val vara = variables.at(literal)
            if (vara.value == -1) then
              if (literalToProp == -1) then
                literalToProp = literal
              else
                loop = false
              end
            else
              if (literal % 2 + vara.value == 1) then
                loop = false
              else
                unitClauseV.push(literal)
              end
            end
            j = j + 1
          end
          if ((literalToProp != -1) && loop) then
            var value = -1
            val unitClause = new Clause(unitClauseV.toArray())
            val index = literalToVarIndex(literalToProp)
            if (literalToProp%2 == 0) then
              value = 1
            else
              value = 0
            end
            noConflict = this.instance.implicationGraph.addImplication(unitClause, dL, value, index)
            if (noConflict) then
              this.assignments.assignVariable(literalToProp, value, dL, true)
              literalsToAssign.push(literalToProp)
              popCounter = popCounter + 1
              didPropagate = true
            end
          end
          i = i + 1
        end
      end
    end
    WAMP("Leave CDCL.unitPropagation()")
    noConflict
  end

  def private conflictAnalysis(dL : int) : int
    val conflictNode = this.instance.implicationGraph.getLatestConflict()
    val conflictLiterals = this.DFS(conflictNode)
    val clauseToconvertToConflict = new Clause(conflictLiterals.toArray())
    val conflictClause = this.assignments.convertClauseToConflictClause(clauseToconvertToConflict)
    var backtrackDL = -1
    this.instance.addClause(conflictClause)
    this.heuristics.addConflictClause(conflictClause)
    this.latestConflictClause = conflictClause
    var vara = conflictClause.literals.at(0)
    if (vara % 2 == 0) then
      vara += 1
    else
      vara -= 1
    end
    if ((conflictClause.literals.size() == 1) && ((this.assignments.variables.at(vara)).setByBranching)) then
      backtrackDL = (this.assignments.variables.at(vara)).dL
    else
      repeat i <- conflictClause.literals.size() do
        val variables = this.assignments.getVariables()
        val literal = conflictClause.literals.at(i)
        val variable = variables.at(literalToVarIndex(literal))
        if ((variable.dL > backtrackDL) && (variable.dL != dL)) then
          backtrackDL = variable.dL
        end
      end
    end
    backtrackDL - 1
  end

  def DFS(conflictNode : Vector[Implication]) : Vector[int]
    val toBeConflictClause = new Vector[int](10)
    val implicationGraph = this.instance.implicationGraph.implicationGraph
    val variables = this.assignments.getVariables()

    val stack = new Vector[int](10)
    stack.push(conflictNode.at(0).variableIndex)

    while (stack.size() > 0) do
      val varIndex = stack.pop()
      val implications = implicationGraph.at(varIndex)
      val vara = variables.at(varIndex)
      if (not vara.visited) then
        vara.visited = true
        repeat i <- implications.size() do
          val implication = implications.at(i)
          val unitClause = implication.unitClause
          repeat j <- unitClause.literals.size() do
            val literal = unitClause.literals.at(j)
            val imp = implicationGraph.at(literalToVarIndex(literal))
            if (imp.size() == 0) then
              var unique = true
              var k = 0
              val kLimit = toBeConflictClause.size()
              while (unique && (k < kLimit)) do
                if (toBeConflictClause.at(k) == literal) then
                  unique = false
                end
                k = k + 1
              end
              if (unique) then
                toBeConflictClause.push(literal)
              end
            else
              stack.push(literalToVarIndex(literal))
            end
          end
        end
      end
    end
    repeat i <- variables.size() do
      val vara = variables.at(i)
      vara.visited = false
    end
    toBeConflictClause
  end

  def variableSelectionToString() : unit
    this.heuristics.variableSelectionToString()
  end

  def valueSelectionToString() : unit
    this.heuristics.valueSelectionToString()
  end
end
