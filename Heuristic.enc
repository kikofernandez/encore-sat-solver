module Heuristic

import Util
import Assignments
import Clause
import VVector
import Vector
import Array


EMBED
#include <time.h>
#include <stdlib.h>
int64_t getRandomValue();

BODY

  int64_t getRandomValue() {
    srand(time(NULL)); //TODO seed only once
    int64_t r = rand();
    return r % 2;
  }


END

linear class Heuristic
  val valueSelection : int
  val variableSelection : int
  val branchOnVariable : bool

  val vsidsCounters : [real]
  val vsidsMax : int -- How many new clauses before decaying variables

  var vsidsCounter : int

  def init(valueSelection : int, variableSelection : int, nVars : int, clauses : RVector[Clause]) : unit
    this.variableSelection = variableSelection
    this.valueSelection = valueSelection
    if(((this.variableSelection > 1) && (this.variableSelection < 4)) || (this.variableSelection == 6)) then
      if(this.valueSelection <  2) then
        println("Illegal combination of value {} and variable {} heuristics.", valueSelection, variableSelection)
        exit(-1)
      end
    end
    if (variableSelection == 6) then -- VSIDS
      this.vsidsCounter = 0
      this.vsidsMax = 50
      this.vsidsCounters = new [real](nVars)
      repeat i <- clauses.size() do
        val clause = clauses.at(i)
        val literals = clause.literals
        repeat j <- |literals| do
          this.vsidsCounters(literals(j) / 2) = this.vsidsCounters(literals(j) / 2) + 1.0
        end
      end
    end
  end

  def getVariableSelection(variables : RArray[Variable], clauses : Vector[Clause]) : [int]
    match this.variableSelection with
      {- Literal Forward -}
      case 0 => this.forwardLiteralInOrder(variables, clauses)
      {- Literal Backwards -}
      case 1 => this.backwardsLiteralInOrder(variables, clauses)
      {- Variable Forward -}
      case 2 => this.forwardVariableInOrder(variables)
      {- Variable Backwards -}
      case 3 => this.backwardsVariableInOrder(variables)
      {- Largest unassigned clause -}
      case 4 => this.largestUnassignedClause(variables, clauses)
      {- Smallest unassigned clause -}
      case 5 => this.smallestUnassignedClause(variables, clauses)
      {- VSIDS -}
      case 6 => this.vsids(variables)
    end
  end


  def valueSelectionToString() : unit
    match this.valueSelection with
      {- Value heuristic is always set literal false -}
      case 0 => println("Value heuristic: Always set literal false")
      {- Value heuristic is always set literal true -}
      case 1 => println("Value heuristic: Always set literal true")
      {- Random heuristic -}
      case 2 => println("Value heuristic: Random")
      {- Aways set variable to false -}
      case 3 =>  println("Value heuristic: Always set variable to false")
      {- Aways set variable to true -}
      case 4 =>  println("Value heuristic: Always set variable to true")
    end
  end

  -- TODO: debug method
  def variableSelectionToString() : unit
    match this.variableSelection with
      case 0 => println("Variable heuristic: Forward in order literal selection")
      case 1 =>  println("Variable heuristic: Backwards in order literal selection")
      case 2 =>  println("Variable heuristic: Forward in order variable selection")
      case 3 =>  println("Variable heuristic: Backwards in order variable selection")
      case 4 =>  println("Variable heuristic: Largest number of unassigned variables in clause")
      case 5 =>  println("Variable heuristic: Smallest number of unassigned variables in clause")
      case 6 =>  println("Variable heuristic: VSIDS")
    end
  end

  -- TODO: pure method that relies on internal state
  def getValueSelection(literal : int) : int
    match this.valueSelection with
      {- Value heuristic is always set literal false -}
      case 0 => literal % 2
      {- Value heuristic is always set literal true -}
      case 1 => (literal + 1) % 2
      {- Random heuristic -}
      case 2 => EMBED (int) getRandomValue(); END
      {- Value heuristic is always set variable false -}
      case 3 => 0
      {- Value heuristic is always set variable true -}
      case 4 => 1
    end
  end

  -- Only for VSIDS
  -- TODO: contains mutation. otherwise the class could have read mode
  def addConflictClause(clause : Clause) : unit
    if (this.variableSelection == 6) then
      val literals = clause.literals
      repeat j <- |literals| do
        this.vsidsCounters(literals(j) / 2) = this.vsidsCounters(literals(j) / 2) + 1.0
      end
      this.vsidsCounter = this.vsidsCounter + 1
      if (this.vsidsCounter == this.vsidsMax) then
        this.vsidsCounter = 0
        repeat i <- |this.vsidsCounters| do
          this.vsidsCounters(i) = this.vsidsCounters(i) * 0.5
        end
      end
    end
  end

  -- TODO: pure function but relies on internal state
  def private vsids(variables : RArray[Variable]) : [int]
    var cur = 0.0
    var maxI = -1
    var max = cur
    repeat i <- |this.vsidsCounters| do
      cur = this.vsidsCounters(i)
      if ((cur >= max) && ((variables.at(i)).value == -1)) then
        max = cur
        maxI = i
      end
    end
    [maxI, -1]
  end

  -- TODO: pure function
  def private forwardVariableInOrder(variables : RArray[Variable]) : [int]
    var index = -1
    var loop = true
    var i = 0
    val iLimit = variables.size()
    val literal = -1
    while (loop && (i < iLimit)) do
      val current = variables.at(i)
      if (current.value == -1) then
        loop  = false
        index = i
      end
      i = i + 1
    end
    [index, literal]
  end

  -- TODO: pure function
  def private backwardsVariableInOrder(variables : RArray[Variable]) : [int]
    var index = -1
    var loop = true
    var i = variables.size() - 1
    val iLimit = 0
    val literal = -1
    while (loop && (i >= iLimit)) do
      val current = variables.at(i)
      if(current.value == -1) then
        loop  = false
        index = i
      end
      i = i - 1
    end
    [index, literal]
  end

  -- TODO: pure function
  def private backwardsLiteralInOrder(variables : RArray[Variable], clauses : Vector[Clause]) : [int]
    var index = -1
    var loop = true
    var i = clauses.size() - 1
    val iLimit = 0
    var literal = -1
    while (loop && (i >= iLimit)) do
      val clause = clauses.at(i)
      val literals = clause.literals
      var j = |literals| - 1
      val jLimit = 0
      while (loop && (j >= jLimit)) do
        literal = literals(j)
        index = literalToVarIndex(literal)
        val vara = variables.at(index)
        if (vara.value == -1) then
          loop = false
        end
        j = j - 1
      end
      i = i - 1
    end
    [index, literal]
  end

  -- TODO: Pure function, doesn't cause mutation
  def private forwardLiteralInOrder(variables : RArray[Variable], clauses : Vector[Clause]) : [int]
    var index = -1
    var loop = true
    var i = 0
    val iLimit = clauses.size()
    var literal = -1
    while (loop && (i < iLimit)) do
      val clause = clauses.at(i)
      val literals = clause.literals
      var j = 0
      val jLimit = |literals|
      while (loop && (j < jLimit)) do
        literal = literals(j)
        index = literalToVarIndex(literal)
        val vara = variables.at(index)
        if (vara.value == -1) then
          loop = false
        end
        j = j + 1
      end
      i = i + 1
    end
    [index, literal]
  end

  -- TODO: pure function
  def private nUnassignedLiterals(clause : Clause, variables : RArray[Variable]) : int
    var nUnassigned = 0
    repeat i <- |clause.literals| do
      if((variables.at(literalToVarIndex(clause.literals(i)))).value == -1) then
        nUnassigned = nUnassigned + 1
      end
    end
    nUnassigned
  end

  -- TODO: pure function
  def private getUnassignedVariable(clause : Clause, variables : RArray[Variable]) : [int]
    var index = 0
    var l = 0
    var shouldBreak = false
    var i = 0
    while ((i < |clause.literals|) && (shouldBreak == false)) do
      index = literalToVarIndex(clause.literals(i))
      if((variables.at(index)).value == -1) then
        l = clause.literals(i)
        shouldBreak = true
      end
      i = i + 1
      if(i == |clause.literals|) then
        shouldBreak = true
      end
    end
    [index,l]
  end

  -- TODO: pure function when `this.nUnassignedLiterals(clauses.at(i),variables)`
  --       is extracted into its own function -- this method is also pure.
  def private largestUnassignedClause(variables : RArray[Variable], clauses : Vector[Clause]) : [int]
    var i = 0
    val iLimit = clauses.size()
    var final_clause = Nothing : Maybe[Clause]
    var number_of_unassigned_literals = -1
    var index = -1
    var literal = -1
    while (i < iLimit) do
      val current_unassigned = this.nUnassignedLiterals(clauses.at(i),variables)
      if((number_of_unassigned_literals < current_unassigned) && (current_unassigned >= 1)) then
        final_clause = Just(clauses.at(i))
        number_of_unassigned_literals = current_unassigned
      end
      i = i + 1
    end

    literal = this.getUnassignedVariable(unjust(final_clause),variables)(1)
    index = this.getUnassignedVariable(unjust(final_clause),variables)(0)
    [index, literal]
  end

  -- TODO: pure function when `this.nUnassignedLiterals(clauses.at(i),variables)` et al
  --       are extracted into their own function -- the methods that call on `this.XXX`
  --       are all pure, i.e. depend only on their inputs
  def private smallestUnassignedClause(variables : RArray[Variable], clauses : Vector[Clause]) : [int]
    var i = 0
    val iLimit = clauses.size()
    var final_clause = Nothing : Maybe[Clause]
    var number_of_unassigned_literals = 9223372036854775807
    var index = -1
    var literal = -1
    while (i < iLimit) do
      val current_unassigned = this.nUnassignedLiterals(clauses.at(i),variables)
      if((number_of_unassigned_literals > current_unassigned) && (current_unassigned >= 1)) then
        final_clause = Just(clauses.at(i))
        number_of_unassigned_literals = current_unassigned
      end
      i = i + 1
    end

    literal = this.getUnassignedVariable(unjust(final_clause),variables)(1)
    index = this.getUnassignedVariable(unjust(final_clause),variables)(0)
    [index, literal]
  end
end
